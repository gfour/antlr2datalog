#include "schema.dl"
#include "base-logic.dl"

// Only named function definitions, no lambdas.
BASE_FunctionDefinition(statId, name, LOCATION(path, line, pos)) :-
  db_LUA.Stat_funcname(statId, fnId),
  db_LUA.Funcname_NAME(fnId, _, _, name, line, _, _, pos),
  ID_PATH(fnId, path).

// No distinction between declarations and definitions.
BASE_FunctionDeclaration(fd, name, location) :-
  BASE_FunctionDefinition(fd, name, location).

BASE_StringConstant(str, LOCATION(path, line, pos)) :-
  db_LUA.String_NORMALSTRING(id, _, str, line, _, _, pos),
  ID_PATH(id, path).

BASE_isScope(id) :- db_LUA.isFuncbody(id).
BASE_isScope(id) :- db_LUA.isBlock(id).

// Variable declarations from function parameters.
BASE_VariableDeclaration(id, v, LOCATION(path, line, pos)) :-
  db_LUA.Funcbody_parlist(funcBody, pl),
  db_LUA.Parlist_namelist(pl, nl),
  db_LUA.Namelist_NAME(nl, id, _, v, line, _, _, pos),
  ID_PATH(funcBody, path).
// Local variable assignments.
BASE_VariableDeclaration(id, v, LOCATION(path, line, pos)) :-
  db_LUA.Stat_varlist(stat, vl),
  db_LUA.Varlist_var(vl, var, _),
  db_LUA.Var_NAME(var, id, v, line, _, _, pos),
  ID_PATH(stat, path).
