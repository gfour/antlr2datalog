#include "schema.dl"
#include "base-logic.dl"
#include "patterns.dl"

// The function-declarator part of both declarations and definitions of functions.
.decl FunctionDeclarator(id: Id, name: symbol, location: Loc)
.output FunctionDeclarator
FunctionDeclarator(dd2_id, name, LOCATION(path, line, pos)) :-
  db_C.DirectDeclarator_Identifier(dd_id, _, name, line, _, _, pos),
  BASE_ParentOf(dd2_id, dd_id),
  db_C.DirectDeclarator_LeftParen(dd2_id, _, "(", _, _, _, _),
  db_C.DirectDeclarator_RightParen(dd2_id, _, ")", _, _, _, _),
  ID_PATH(dd2_id, path).

BASE_FunctionDeclaration(extDeclId, name, location) :-
  FunctionDeclarator(dd2_id, name, location),
  BASE_RecParentOf(declId, dd2_id),
  db_C.isDeclaration(declId),
  BASE_ParentOf(extDeclId, declId),
  db_C.isExternalDeclaration(extDeclId).

BASE_FunctionDefinition(fd, name, location) :-
  FunctionDeclarator(dd2_id, name, location),
  BASE_RecParentOf(fd, dd2_id),
  db_C.isFunctionDefinition(fd).

BASE_StringConstant(str, LOCATION(path, line, pos)) :-
  db_C.PrimaryExpression_StringLiteral(id, _, _, str, line, _, _, pos),
  ID_PATH(id, path).

BASE_isScope(id) :- db_C.isCompoundStatement(id).
BASE_isScope(id) :- db_C.isStructDeclarationList(id).

// Variable declarations from function parameters.
BASE_VariableDeclaration(id, v, LOCATION(path, line, pos)) :-
  db_C.DirectDeclarator_Identifier(dd, id, v, line, _, _, pos),
  UP_PATH2(dd, db_C.isDeclarator, db_C.isParameterDeclaration, pd),
  ID_PATH(pd, path).
// Local variable declarations.
BASE_VariableDeclaration(id, v, LOCATION(path, line, pos)) :-
  db_C.DirectDeclarator_Identifier(dd, id, v, line, _, _, pos),
  UP_PATH2(dd, db_C.isDeclarator, db_C.isInitDeclarator, idecl),
  ID_PATH(idecl, path).
