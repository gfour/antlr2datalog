#include "schema.dl"
#include "base-logic.dl"
#include "patterns.dl"
#include "areas.dl"

// The function-declarator part of both declarations and definitions of functions.
.decl FunctionDeclarator(id: Id, name: symbol, location: Loc)
.output FunctionDeclarator
FunctionDeclarator(dd2_id, name, LOCATION(artifact, path, line, pos)) :-
  db_C.DirectDeclarator_Identifier(dd_id, terminal),
  BASE_Terminal(terminal, name, line, _, _, pos),
  BASE_ParentOf(dd2_id, dd_id),
  db_C.DirectDeclarator_LeftParen(dd2_id, _),
  db_C.DirectDeclarator_RightParen(dd2_id, _),
  ID_PATH(artifact, dd2_id, path).

BASE_FunctionDeclaration(extDeclId, name, location) :-
  FunctionDeclarator(dd2_id, name, location),
  BASE_RecParentOf(declId, dd2_id),
  db_C.isDeclaration(declId),
  BASE_ParentOf(extDeclId, declId),
  db_C.isExternalDeclaration(extDeclId).

BASE_FunctionDefinition(fd, name, location) :-
  FunctionDeclarator(dd2_id, name, location),
  BASE_RecParentOf(fd, dd2_id),
  db_C.isFunctionDefinition(fd).

.decl CPP_Function_Specifier(fd: Id, specifier: Id)
.output CPP_Function_Specifier

// Group together different kinds of C++ function specifiers.
CPP_Function_Specifier(fd, specifier) :-
  BASE_FunctionDefinition(fd, _, _),
  db_C.FunctionDefinition_declarationSpecifiers(fd, declSpecs),
  db_C.DeclarationSpecifiers_declarationSpecifier(declSpecs, declSpec, _),
  ( db_C.DeclarationSpecifier_functionSpecifier(declSpec, specifier)
  ; db_C.DeclarationSpecifier_storageClassSpecifier(declSpec, specifier)
  ; db_C.DeclarationSpecifier_typeQualifier(declSpec, specifier)).

BASE_Modifier(fd, mod) :-
  CPP_Function_Specifier(fd, specifier),
  BASE_ParentOf(specifier, terminal),
  BASE_Terminal(terminal, mod, _, _, _, _).

BASE_StringConstant(str, LOCATION(artifact, path, line, pos)) :-
  db_C.PrimaryExpression_StringLiteral(id, terminal, _),
  BASE_Terminal(terminal, str, line, _, _, pos),
  ID_PATH(artifact, id, path).

BASE_isScope(id) :- db_C.isCompoundStatement(id).
BASE_isScope(id) :- db_C.isStructDeclarationList(id).

.decl C_Function_ParameterDecl(fd: db_C.FunctionDefinition, pd: db_C.ParameterDeclaration, idx: number)
.output C_Function_ParameterDecl

C_Function_ParameterDecl(fd, pd, i) :-
  db_C.ParameterList_parameterDeclaration(_, pd, i),
  BASE_RecParentOf(ptl, pd),
  UP_PATH4(ptl, db_C.isParameterTypeList, db_C.isDirectDeclarator, db_C.isDeclarator, db_C.isFunctionDefinition, fd).

BASE_FunctionParameter(fd, id, v, idx, LOCATION(artifact, path, line, pos)) :-
  C_Function_ParameterDecl(fd, pd, idx),
  db_C.DirectDeclarator_Identifier(dd, id),
  BASE_Terminal(id, v, line, _, _, pos),
  UP_PATH2(dd, db_C.isDeclarator, db_C.isParameterDeclaration, pd),
  ID_PATH(artifact, pd, path).

// Local variable declarations.
BASE_VariableDeclaration(id, v, LOCATION(artifact, path, line, pos)) :-
  db_C.DirectDeclarator_Identifier(dd, id),
  BASE_Terminal(id, v, line, _, _, pos),
  UP_PATH2(dd, db_C.isDeclarator, db_C.isInitDeclarator, idecl),
  ID_PATH(artifact, idecl, path).

BASE_Type(decl, name, LOCATION(artifact, path, line, pos)) :-
  db_C.StructOrUnionSpecifier_Identifier(spec, terminal),
  db_C.StructOrUnionSpecifier_LeftBrace(spec, _),
  BASE_Terminal(terminal, name, line, _, _, pos),
  UP_PATH4(spec, db_C.isTypeSpecifier, db_C.isDeclarationSpecifier, db_C.isDeclarationSpecifiers, db_C.isDeclaration, decl),
  ID_PATH(artifact, decl, path).
